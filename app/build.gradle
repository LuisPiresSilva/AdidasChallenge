plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'com.google.firebase.crashlytics'
}
apply from: "$project.rootDir/tools/git-version.gradle"
apply from: "$project.rootDir/tools/versions.gradle"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.adidas.luissilva"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        buildConfigField ("long", "API_TIMEOUT", "30")
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        dataBinding = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        prod {
            initWith release
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard.pro"
            manifestPlaceholders += [
                appIcon: "@mipmap/ic_launcher",
                appIconRound: "@mipmap/ic_launcher_round"
            ]
        }

        qa {
            initWith debug
            debuggable true
            applicationIdSuffix versions.appIdSuffixQa
            manifestPlaceholders += [
                appIcon: "@drawable/ic_launcher_qa",
                appIconRound: "@drawable/ic_launcher_round_qa"
            ]
        }
        
        dev {
            initWith debug
            debuggable true
            applicationIdSuffix versions.appIdSuffixDev
            manifestPlaceholders += [
                appIcon: "@drawable/ic_launcher_dev",
                appIconRound: "@drawable/ic_launcher_round_dev"
            ]
        }
    }
}

android.variantFilter { variant ->
    if(variant.buildType.name.endsWith('release') || variant.buildType.name.endsWith('debug')) {
variant.setIgnore(true)
    }
}

apply from: "$project.rootDir/tools/flavors.gradle"
apply from: "$project.rootDir/tools/dependencies.gradle"

dependencies {
    implementation 'com.google.android.gms:play-services-fitness:19.0.0'
    implementation 'com.google.android.gms:play-services-auth:18.1.0'
}

//TODO add google-services.json before uncomment
apply plugin: 'com.google.gms.google-services'